plugins {
    id 'java'
    id 'application'
}

dependencies {
    implementation project(':bolt-schema')

    implementation "com.lmax:disruptor:${versions.disruptor}"
    implementation "io.aeron:aeron-driver:${versions.aeron}"
    implementation "io.aeron:aeron-client:${versions.aeron}"

    compileOnly "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    testImplementation "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
}

application {
    mainClass = 'com.cmex.bolt.Bolt'
}

// 常规测试任务 - 使用默认内存配置
test {
    useJUnitPlatform()

    // 排除性能测试类，避免普通测试时消耗大量内存
    exclude '**/*OrderPerformanceTestWithHTMLReport*'
}

tasks.register('performanceTest', Test) {
    group = 'performance'

    useJUnitPlatform()
    filter {
        includeTestsMatching "com.cmex.bolt.performance.*"
    }

    // 高性能测试的专用JVM配置
    jvmArgs = [
            '-Xmx4g',
            '-Xms4g',
            '-XX:MaxDirectMemorySize=8g',
    ]

    // 设置测试超时（性能测试需要更长时间）
    systemProperty 'junit.jupiter.execution.timeout.default', '600s'

}

tasks.register('fatJar', Jar) {
    archiveFileName = 'bolt-spot-all-in-one.jar'
    manifest {
        attributes 'Main-Class': 'com.cmex.bolt.spot.SpotServer'
    }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}